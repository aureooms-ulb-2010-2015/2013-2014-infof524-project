model "Burglar (index set)"
	uses "mmxprs"


	(! variables !)

	declarations
		WTMAX = 102                   ! Maximum weight allowed
		ITEMS = {"camera", "necklace",
		"vase", "picture", "tv",
		"video", "chest", "brick"}    ! Index range for items
		VALUE: array(ITEMS) of real   ! Value of items
		WEIGHT: array(ITEMS) of real  ! Weight of items
		take: array(ITEMS) of mpvar   ! 1 if we take item i; 0 otherwise
	end-declarations


	(! data !)

	VALUE("camera")   :=  15; WEIGHT("camera")   :=  2
	VALUE("necklace") := 100; WEIGHT("necklace") := 20
	VALUE("vase")     :=  90; WEIGHT("vase")     := 20
	VALUE("picture")  :=  60; WEIGHT("picture")  := 30
	VALUE("tv")       :=  40; WEIGHT("tv")       := 40
	VALUE("video")    :=  15; WEIGHT("video")    := 30
	VALUE("chest")    :=  10; WEIGHT("chest")    := 60
	VALUE("brick")    :=   1; WEIGHT("brick")    := 10


	(! objective !)

	MaxVal:= sum(i in ITEMS) VALUE(i) * take(i)


	(! constraints !)
	
	sum(i in ITEMS) WEIGHT(i) * take(i) <= WTMAX        ! Weight restriction

	forall(i in ITEMS) take(i) is_binary                ! All variables are 0/1


	(! solve !)

	maximize(MaxVal)


	(! output !)

	writeln("Solution:")
	writeln(" profit : ", getobjval)
	forall(i in ITEMS) writeln(" take(", i, "): ", getsol(take(i)))

end-model